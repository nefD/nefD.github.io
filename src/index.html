<html>
<body>
<!-- vertex shader -->
<script id="plane-vs" type="x-shader/x-vertex">
  #ifdef GL_ES
  precision mediump float;
  #endif
  // those are the mandatory attributes that the lib sets
  attribute vec3 aVertexPosition;
  attribute vec2 aTextureCoord;
  // those are mandatory uniforms that the lib sets and that contain our model view and projection matrix
  uniform mat4 uMVMatrix;
  uniform mat4 uPMatrix;
  // our texture matrix that will handle image cover
  uniform mat4 uTextureMatrix0;
  // pass your vertex and texture coords to the fragment shader
  varying vec3 vVertexPosition;
  varying vec2 vTextureCoord;

  void main() {
    vec3 vertexPosition = aVertexPosition;
    gl_Position = uPMatrix * uMVMatrix * vec4(vertexPosition, 1.0);
    // set the varyings
    // here we use our texture matrix to calculate the accurate texture coords
    vTextureCoord = (uTextureMatrix0 * vec4(aTextureCoord, 0.0, 1.0)).xy;
    vVertexPosition = vertexPosition;
  }
</script>
<!-- fragment shader -->
<script id="plane-fs" type="x-shader/x-fragment">
  #ifdef GL_ES
  precision mediump float;
  #endif

  #define PI 3.14159265358979323846

  // get our varyings
  varying vec3 vVertexPosition;
  varying vec2 vTextureCoord;
  // the uniform we declared inside our javascript
  uniform float uTime;
  uniform float uTimeScale;
  // our texture sampler (default name, to use a different name please refer to the documentation)
  uniform sampler2D uSampler0;
  uniform vec2 uResolution;
  uniform float uScale;
  uniform vec4 uBorderColor;

  #define MAGIC 43758.5453123

  float random (vec2 st) {
    float s = dot(st, vec2(0.400,0.230));
    return -1. + 2. * fract(sin(s) * MAGIC);
  }

  vec4 valueNoise (vec2 p) {
    vec2 i = floor(p);

    float f11 = random(i + vec2(0., 0.));
    float f12 = random(i + vec2(0., 1.));
    float f21 = random(i + vec2(1., 0.));
    float f22 = random(i + vec2(1., 1.));

    return vec4(f11, f12, f21, f22);
  }

  vec2 scale (vec2 p, float s) {
    return p * s;
  }

  float interpolate (float t) {
//     return t * t * (3. - 2. * t); // smoothstep
    return t * t * t * (10. + t * (6. * t - 15.)); // smootherstep
  }

  float noise (vec2 p) {
    vec4 v = valueNoise(p);
    vec2 f = fract(p);
    float t = interpolate(f.x);
    float u = interpolate(f.y);

    // linear interpolation
    return mix(
      mix(v.x, v.z, t),
      mix(v.y, v.w, t),
      u
    ) * .5 + .5;
  }

  vec2 rotate2D(vec2 _st, float _angle){
    _st -= 0.5;
    _st =  mat2(cos(_angle),-sin(_angle),
    sin(_angle),cos(_angle)) * _st;
    _st += 0.5;
    return _st;
  }

  void main() {
    vec2 textureCoord = vTextureCoord;
    float tx = textureCoord.x;
    float ty = textureCoord.y;
    float breakY = 0.5;
    float yp = ty / breakY;
    float breakLeft = 0.1;
    float breakRight = 0.9;
    vec2 st = gl_FragCoord.xy/uResolution;
    st.y -= (uTime / (75.0 * uTimeScale));
    st.x += cos(tx * 15.) * sin(ty * 23.) * cos(uTime / (20. * uTimeScale)) / 10.;
    float z = noise(scale(st, uScale));
    float threshold = 0.5 * yp;

    if (tx <= breakLeft && ty < breakY) {
      threshold -= (1.0 - tx / breakLeft) * (1.0 - yp);
    } else if (tx >= breakRight && ty < breakY) {
      threshold -= ((tx - breakRight) / (1.0 - breakRight)) * (1.0 - yp);
    }

    if (z < 0.5) {
      z = z * (1.0 - ty);
    } else {
      z = z * (1.0 + ty);
    }

    if (z <= threshold || z >= (1.0 - threshold)) {
      float newY = (breakY * 0.5) + ((breakY * 0.5) * yp);
      if (ty < breakY) {
        ty = newY;
      }

      float refraction = 1.1;
      float rMod = max(0., 1.0 - (ty * 2.));
      float xMod = ((abs(z - 0.5) * refraction) + (tx - 0.5)) * (1.0 - ty * 1.5);
      tx += (xMod * rMod);

      float yMod = ((abs(z - 0.5) * refraction) + (ty - 0.5)) * (1.0 - ty * 1.5);
      ty += (yMod * rMod);

      float borderThreshold = min(1., 0.8 + max(0., (ty - 0.5)));

      if (borderThreshold > 0. && ((z <= threshold && z > (threshold * borderThreshold)) || (z >= (1.0 - threshold)) && z < (1.0 - (threshold * borderThreshold)))) {
          gl_FragColor = uBorderColor;
      } else {
        float distance = 1.;
        if (z <= threshold) {
          distance = 1.;
        } else {
          distance = (z - (1.0 - threshold)) / threshold;
        }
        distance = min(1., distance * (yp * 1.));
        gl_FragColor = mix(uBorderColor, texture2D(uSampler0, vec2(tx, ty)), distance);
      }
    } else {
      gl_FragColor = vec4(vec3(0.), 1.);
    }
  }
</script>

<div id="canvas"></div>

<div class="plane">
  <div id="text">december09</div>

  <img src="images/ResevoirDogs.jpg" alt="">
</div>

<script src="index.js"></script>

</body>
</html>
